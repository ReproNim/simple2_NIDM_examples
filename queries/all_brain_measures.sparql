# This query simply returns the brain measurements without dependencies on other 
# demographics/assessment measures.

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix ndar: <https://ndar.nih.gov/api/datadictionary/v2/dataelement/>
prefix nidm: <http://purl.org/nidash/nidm#>
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?ID ?tool ?softwareLabel ?federatedLabel ?laterality ?volume
where {
        ?tool_act a prov:Activity ;
                    prov:qualifiedAssociation [prov:agent [nidm:NIDM_0000164 ?tool]] .
                ?tool_act prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] .
                ?tool_entity prov:wasGeneratedBy ?tool_act ;
                        ?measure ?volume .

                ?measure a/rdfs:subClassOf* nidm:DataElement ;
                            rdfs:label ?softwareLabel;
                            nidm:measureOf <http://uri.interlex.org/base/ilx_0112559> ; #Volume
                            nidm:datumType <http://uri.interlex.org/base/ilx_0738276> . #Volume
                            #nidm:measureOf <http://uri.interlex.org/base/ilx_0111689> . #Thickness
                            #nidm:measureOf <http://purl.obolibrary.org/obo/PATO_0001323> . #Surface Area
                OPTIONAL {?measure nidm:isAbout ?federatedLabel }.
                OPTIONAL {?measure nidm:hasLaterality ?laterality }.
}
ORDER BY ?ID ?softwareLabel
