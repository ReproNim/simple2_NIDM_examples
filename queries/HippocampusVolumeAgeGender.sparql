# This query simply returns the brain volume data without dependencies on other demographics/assessment measures.

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix ndar: <https://ndar.nih.gov/api/datadictionary/v2/dataelement/>
prefix nidm: <http://purl.org/nidash/nidm#>
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ilx: <http://uri.interlex.org/> .


select distinct ?ID ?Gender ?Age ?tool ?softwareLabel ?federatedLabel ?laterality ?volume
where {
 	        ?tool_act a prov:Activity ;
		            prov:qualifiedAssociation [prov:agent [nidm:NIDM_0000164 ?tool]] .
			?tool_act prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] .
			?tool_entity prov:wasGeneratedBy ?tool_act ;
				?measure ?volume .
			
				?measure a/rdfs:subClassOf* nidm:DataElement ;
					 rdfs:label ?softwareLabel;
					 nidm:measureOf <http://uri.interlex.org/base/ilx_0112559> ;
					 nidm:datumType <http://uri.interlex.org/base/ilx_0738276> .
				OPTIONAL {?measure nidm:isAbout ?federatedLabel }.
				OPTIONAL {?measure nidm:hasLaterality ?laterality }.
  			?as_activity prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] . 
						
			# find age data element uuid
			{?age_measure a nidm:PersonalDataElement ;
					nidm:isAbout ilx:ilx_0100400 ;
                    nidm:sourceVariable "AGE_AT_SCAN" .
			}
			# find sex data element uuid
			{?sex_measure a nidm:PersonalDataElement ;
					nidm:isAbout <http://uri.interlex.org/base/ilx_0101292> . #Biological sex
			}
  			?as_entity prov:wasGeneratedBy ?as_activity ;
	        	?age_measure ?Age ;
	        	?sex_measure ?GenderCoded .
                
          	bind(IF((?GenderCoded ="1" || ?GenderCoded ="Male"^^xsd:string), "Male"^^xsd:string,"Female"^^xsd:string) as ?Gender) .
			FILTER (?federatedLabel = <http://purl.obolibrary.org/obo/UBERON_0001954>) #Hippocampus
}