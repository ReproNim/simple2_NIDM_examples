prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix ndar: <https://ndar.nih.gov/api/datadictionary/v2/dataelement/>
prefix fsl: <http://purl.org/nidash/fsl#>
prefix nidm: <http://purl.org/nidash/nidm#>
prefix onli: <http://neurolog.unice.fr/ontoneurolog/v3.0/instrument.owl#>
prefix freesurfer: <https://surfer.nmr.mgh.harvard.edu/>
prefix dx: <http://ncitt.ncit.nih.gov/Diagnosis>
prefix ants: <http://stnava.github.io/ANTs/> 
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix ncicb: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
prefix ncit: <http://ncitt.ncit.nih.gov/>

select distinct ?study ?ID ?Age ?dx ?Gender ?FIQ ?PIQ ?VIQ 	?tool ?softwareLabel ?federatedLabel ?laterality ?volume
where {
            ?measure a/rdfs:subClassOf nidm:DataElement;
                 rdfs:label ?softwareLabel;
                 nidm:measureOf <http://uri.interlex.org/base/ilx_0112559> ;
                 nidm:datumType <http://uri.interlex.org/base/ilx_0738276> .
            OPTIONAL {?measure nidm:isAbout ?federatedLabel }.
            OPTIONAL {?measure nidm:hasLaterality ?laterality }.

            ?tool_entity a prov:Entity;
                ?measure ?volume .

            ?tool_act a prov:Activity ;
                    prov:qualifiedAssociation [prov:agent [nidm:NIDM_0000164 ?tool]] ;
                    prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] .
            ?tool_entity prov:wasGeneratedBy ?tool_act .
			
			?as_activity prov:qualifiedAssociation [prov:agent ?agent] ; 
					dct:isPartOf/dct:isPartOf [dctypes:title ?study] .
			?agent ndar:src_subject_id ?ID .
				
			
			# find age data element uuid
			{?age_measure a nidm:DataElement ;
					nidm:isAbout ncicb:Age .
			}
			# find sex data element uuid
			{?sex_measure a nidm:DataElement ;
					nidm:isAbout ndar:gender .
			}
			# find diagnosis data element uuid
			{?DX_measure a nidm:DataElement ;
					nidm:isAbout ncit:Diagnosis .
			
			}
			# find fiq data element uuid
  			{?FIQ_measure a nidm:DataElement ;
					nidm:isAbout <https://github.com/dbkeator/nidm-local-terms/issues/2> .
			}
			# find piq data element uuid
			{?PIQ_measure a nidm:DataElement ;
					nidm:isAbout <https://github.com/dbkeator/nidm-local-terms/issues/4> .
			}
			# find viq data element uuid
			{?VIQ_measure a nidm:DataElement ;
					nidm:isAbout <https://github.com/dbkeator/nidm-local-terms/issues/3> .
			}
			
  			?as_entity prov:wasGeneratedBy ?as_activity ;
	        	?age_measure ?Age;
	        	?sex_measure ?GenderCoded ;
                ?FIQ_measure ?FIQ ;
                ?PIQ_measure  ?PIQ ;
                ?VIQ_measure ?VIQ ;
            	?DX_measure ?dx .
			
			bind(IF((?GenderCoded ="1" || ?GenderCoded ="Male"^^xsd:string), "Male"^^xsd:string,"Female"^^xsd:string) as ?Gender) .
			
            
					
				
} order by ?study

